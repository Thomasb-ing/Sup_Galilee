*NAME OF THE INPUT FILE (RESTITUTION)
OPTI 'REST' 'formation_debutant_1_maillage.sauv';

*LOADING INTO MEMORY
REST;

*MATERIAL PROPERTIES
CONDUMAT = 210.;
CAPAMAT = 900.;
RHOMAT = 2700.;

*INITIAL AND REFERENCE TEMPERATURE
T0 = 25.;

*THERMAL MODEL (CONDUCTION) UNIFORM AND STATIONARY MATERIAL PROPERTIES
MOT = MODE SU 'THERMIQUE';
MAT = MATE MOT 'K' CONDUMAT 'C' CAPAMAT 'RHO' RHOMAT 'TINI' T0;

*CONDUCTIVITY MATRIX CALCULATION (FIRST MEMBER)
CON = COND MOT MAT;

*BOUNDARY CONDITIONS : PRESCRIBED TEMPERATURE ON THE HOLE
BLT = BLOQ CE 'T';

*NODAL HEAT FLUX (LOADINGS) FOR CONSTRAINTS
TMAX = 250.;
FLT1 = DEPI BLT TMAX;

*PRESCRIBED HEAT FLUX ON THE LEFT SIDE
FLT2 = FLUX MOT LIGA -3.5E4;

*TEMPERATURE FIELD CALCULATION USING 'RESO'
TCON1 = RESO (CON ET BLT) (FLT1 ET FLT2);

*AFFICHAGE : TRAC SU TCON1

*CONVECTION MODEL
MOC =MODE LHAUT 'THERMIQUE' 'CONVECTION' ;
MAC = MATE MOC 'H' 100;

*CONDUCTIVITY MATRIX
*FIRST MEMBER FOR CONVECTION
CONH = COND MOC MAC;

*EQUIVALENT NODAL HEAT FLUX VECTOR

*SECOND MEMBER FOR CONVECTION

CHTC = MANU 'CHPO' LHAUT 'T' T0 ;
FLH = CONV MOC MAC CHTC ;

* SECOND MEMBER FOR THE VOLUME SOURCE

X Y = COOR SU ;
PT1 = X POIN 'SUPERIEUR' 20.E-1 ;
PT2 = (REDU Y PT1) POIN 'INFERIEUR' 5.E-1 ;
ELSOU = SU ELEM 'APPUYE' 'STRICTEMENT' PT2 ;
FLS = SOUR (REDU MOT ELSOU) 1.E5 ELSOU ;

*TEMPERATURE FIELD CALDULATION USING 'RESO'
TCON2 = RESO (CON ET CONH ET BLT) (FLT1 ET FLT2 ET FLH ET FLS);

*DISPLAY THE TEMPERATURE
LISO1 = PROG 0. 'PAS' (TMAX/15.) TMAX;
TRAC TCON2 SU CSU LISO1;
TRAC (TCON2-TCON1) SU CSU LISO1;

FIN;

