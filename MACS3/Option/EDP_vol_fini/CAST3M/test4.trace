 *NAME OF THE INPUT FILE (RESTITUTION) 
 OPTI 'REST' 'formation_debutant_1_maillage.sauv'; 
 OPTI 'REST' 'tableau_temps.sauv'; 
   
 *LOADING INTO MEMORY 
 REST; 
   
 * CARACTERISTIQUES MECANIQUES DU MATERIAU 
 YOUNGMAT = 70.E9; 
 NUMAT = 0.36; 
 ALPHAMAT = 24.E-6; 
 SIGYMAT = 130E6; 
 RHOMAT = 2700.; 
   
 *HYPOTHESE DES CONTRAINTES PLANES 
 OPTI 'MODE' 'PLAN' 'CONT'; 
   
 * MODELE MECANIQUE ELASTIQUE LINEAIRE ISOTROPE 
 MOM1 = MODE SU 'MECANIQUE' 'ELASTIQUE' ; 
 MAM1 = MATE MOM1 'YOUN' YOUNGMAT 'NU' NUMAT 'ALPHA' ALPHAMAT 'TREF' T0 'TALP' T0 ; 
   
 *MATRICE DE RAIDEUR OU RIGIDITE 
   
 RI = RIGI MOM1 MAM1; 
   
 *CONDITIONS AUX LIMITES DE DEPLACEMENT IMPOSES 1er membre 
 *blocage selon x du point A 
 BLMX = BLOQ PA 'UX'; 
 BLMY = BLOQ LBAS 'UY'; 
 RITOT = RI ET BLMX ET BLMY ; 
   
 *FORCES NODALES REPRESENTATIVE DE LA PRESSION 2nd membre 
 TR = PRES 'MASS' MOM1 LHAUT (-100.E6); 
   
 *FORCES NODALES REPRESENTATIVE DE LA PRESSION 2nd  membre 
 ROG = MANU 'CHPO' SU 2 'FX' 0. 'FY' (-9.81 * RHOMAT); 
 FV = CNEQ MOM1 ROG ; 
   
 *RESOLUTION DU SYSTEME 
 U5 = RESO RITOT (TR ET FV); 
   
 *POST TRAITEMENT DEFORMATION CONTRAINTES MAILLAGE DEFORME 
   
 *TRACE DU MAILLAGE DEFORME 
 DEF_5 = DEFO SU U5 150. 'ROUG'; 
 DEF_INI = DEFO SU U5 0. ; 
 TRAC (DEF_INI ET DEF_5) ; 
   
 *TRACE DU CONTOUR DEFORME 
 DEF_5C = DEFO CSU U5 150. 'ROUG'; 
 DEF_INIC = DEFO CSU U5 0. ; 
 TRAC (DEF_5C ET DEF_INIC); 
   
 *EN DEUX COUPS : 
 *CALCUL DES DEFORMATIONS 
 DEF = EPSI MOM1 U5 'LINE'; 
   
 *CONTRAINTES A PARTIR DES DEFORMATIONS 
 SIG = ELAS DEF MOM1 MAM1; 
   
 *EN UN SEUL COUP : 
 *CONTRAINTE A PARTIR DES DEPLACEMENTS 
 SIG =SIGMA U5 MOM1 MAM1; 
   
 *TRACE DES CONTRAINTES 
 TRAC SIG MOM1 CSU 15; 
 *TRACE DES CONTRAINTES SUR CONFIGURATION DERFORMEE 
 DEF_5B = DEFO SU U5 150.; 
 TRAC SIG MOM1 DEF_5B CSU 15; 
   
 *POST TRAITEMENT : EVOLUTION DE LA CONCENTRATION DE CONTRAINTE (LE LONG DU COTE BAS) 
 SIGB = CHAN 'CHPO' SIG MOM1 ; 
 LBASG = LBAS ELEM 'COMP' PB PA; 
 EVSIG = EVOL 'CHPO' SIGB 'SMYY' LBASG; 
 EVK = EVSIG /100.E6; 
 DESS EVK 'TITR' '[5] Concentration de contrainte le long de LBAS'; 
   
   
 *DEFORMAIONS THERMIQUES PURES AU DERNIER CHAMP DE TEMPERATURE 
 *DU CALCUL THERMIQUE  AVEC CONVECTION+ RAYONEMENT 
 T_ini = tab_T . 0 ; 
   
 DELTA_TE =(tab_T .NN) - T_INI; 
 EPT = EPTH DELTA_TE MOM1 MAM1; 
   
 *PSEUDO CONTRAINTES POUR CES DEFORMATIONS THERMIQUES 
 SIT = ELAS EPT MOM1 MAM1; 
   
 *FORCES NODALES POUR CETTE DEFORMATION THERMIQUE 
 FFT = BSIG SIT MOM1 ; 
   
 *RESOLUTION DU SYSTEME LINEAIRE (AJOUT DUN TERME AU 2nd MEMBRE) 
 *DEPLACEMENT, PAR APPEL AU SOLVEUR EN SUPERPOSANT LES FORCES 
 *PUREMENT MECANIQUES ET LES PSEUDP FORCES THERMIQUES 
 U6 = RESO RITOT (FFT ET TR ET FV); 
   
   
 *TRACE DU CONTOUR DEFORME 
 DEF_6 = DEFO SU U6 150. ; 
 DEF_6C = DEFO CSU U6 150. 'ORAN'; 
 DEF_INIC = DEFO CSU U6 0. ; 
 TRAC (DEF_5C ET DEF_6C ET DEF_INIC); 
   
 *Post traitement : maillage déformé, déformations, contraintes 
 *Deformation totales 
 EP = EPSI MOM1 U6 'LINE'; 
 *Deformation elastique 
 EPE = EP - EPT ; 
 *les contraintes calculées a partir des deformations elastiques 
 SIGT = ELAS EPE MOM1 MAM1; 
 TRAC SIGT MOM1 DEF_6 CSU ; 
 FIN ; 
