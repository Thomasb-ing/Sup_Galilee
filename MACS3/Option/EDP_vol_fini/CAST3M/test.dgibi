*GENERAL OPTIONS AND TYPE OF FINITE ELEMENTS
OPTI 'DIME' 2 'ELEM' 'QUA8';

*LENGTHS AND MESH DENSITY DEFINITIONS
LONG = 24.E-1;
HAUT = 10.E-1;
RAYON = 2.E-1;

NLONG = 24;
NHAUT = 4;
NBASG = 10;
NBASD = 10;
NQCG = 8;
NQCD =8;

*CREATION OF POINTS SUPPORTING THE MESH
PA = 0. 0. ;
PB = ((0.5 * LONG) - RAYON) 0.;
PC = (0.5 * LONG) RAYON;
PD = ((0.5 * LONG) + RAYON) 0.;
PE = LONG 0.;
PF = LONG HAUT;
PG = 0. HAUT;
PCEN = (0.5*LONG) 0.;

*STRAIGHT LINES CONSTRUCTION (DROIT OR D)
LIAB = DROI NBASG PA PB;
LIDE = DROI NBASD PD PE;
LIEF = DROI NHAUT PE PF;
LIFG = DROI NLONG PF PG;
LIGA = DROI NHAUT PG PA;

*CONSTRUCTION OF CIRCLES (CERC OU C)
CE1 = CERC NQCG PB PCEN PC;
CE = CERC NQCD CE1 PCEN PD;

*CLOSED CONTOUR OBTAINED FROM THE ELEMENTARY LINES ASSEMBLY
CO = LIAB ET CE ET LIDE ET LIEF ET LIFG ET LIGA;

NHAUT = 10;
NQCG = 20;
PH = (0.5*LONG) HAUT;
LIHG = DROI (NQCG - NHAUT) PH PG;
LIGA = DROI NHAUT PG PA;
CE1 = CERC NQCG PB PCEN PC;
SU1 = REGL 'DINI' 0.05 'DFIN' 0.1 (INVE CE1) (LIHG ET LIGA);
SU2 = SU1 SYME 'DROI' PCEN PH;
SU = SU1 ET SU2;
ELIM SU 1.E-9;
*TRAC ((COUL ROUGE SU1) ET (COUL BLEU SU2));

*HALF-CIRCLE MESH, UPPER AND LINES MESHES RETRIEVAL WITH 'POIN' ET 'ELEM'

CSU = CONT SU;
PCE = SU POIN 'SPHE' PCEN PC 1.E-9;
CE = CSU ELEM 'APPU' 'STRI' PCE;
PLHAUT = SU POIN 'DROI' PF PG 1.E-9;
LHAUT = CSU ELEM 'APPU' 'STRI' PLHAUT;
PLBAS = SU POIN 'DROI' PE PCEN 1.E-9;
LBAS = CSU ELEM 'APPU' 'STRI' PLBAS;

TRAC ((COUL ROUGE CE) ET (COUL BLEU LHAUT) ET (COUL OLIVE LBAS) ET (COUL BLANC CSU) );
TRAC SU;

* NAME OF THE OUTPUT FILE
OPTI 'SAUV' 'formation_debutant_1_maillage.sauv';

*WRITING THE FILE
SAUV;

*END OF THE GIBI PROGRAM
FIN;